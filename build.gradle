apply plugin: 'java'

sourceCompatibility = 1.5
version = '1.0'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.jbehave', name: 'jbehave-core', version: '4.0-beta-11'
    testCompile 'org.jbehave.site:jbehave-site-resources:3.1.1:@zip'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '2.45.0'
    testCompile group: 'com.github.detro.ghostdriver', name: 'phantomjsdriver', version: '1.1.0'
}

sourceSets.test.resources.srcDir 'src/test/java'

test {
    systemProperties = System.getProperties()

    doFirst {
        copy {
            from(zipTree(jarPath("jbehave-core"))) {
                include "style/*"
            }
            into("target/build/jbehave/view")

        }
       copy {
            from(zipTree(jarPath("jbehave-site-resources"))) {
                include "js/**/*"
                include "style/**/*"
               include "images/*"
           }
            into("target/build/jbehave/view")
       }
    }
}

def jarPath(String jarName) {
    configurations.testCompile.find({ it.name.startsWith(jarName) }).absolutePath
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}